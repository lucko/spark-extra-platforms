plugins {
    id 'com.gradleup.shadow' version '8.3.0'
}

tasks.withType(JavaCompile) {
    // override, compile targeting J17
    options.release = 17
}

dependencies {
    implementation "me.lucko:spark-common:${project.baseVersion}-SNAPSHOT"

    implementation 'net.kyori:adventure-text-serializer-legacy:4.14.0'
    compileOnly 'org.geysermc.geyser:api:2.4.2-SNAPSHOT'
    compileOnly 'com.google.guava:guava:29.0-jre'
    compileOnly('org.geysermc.geyser:core:2.4.2-SNAPSHOT') {
        transitive(false)
    }
}

repositories {
    maven { url 'https://repo.opencollab.dev/main/' }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'extension.yml'
    }
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-geyser.jar"

    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    project.applyExcludes(delegate)
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
