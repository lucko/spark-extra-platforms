plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.17'
}

tasks.withType(JavaCompile) {
    options.release = 21
}

java {
    disableAutoTargetJvm()
}

dependencies {
    implementation "me.lucko:spark-common:${project.baseVersion}-SNAPSHOT"
    paperweight.foliaDevBundle("1.21.11-R0.1-SNAPSHOT")
}

repositories {
    maven { url "https://repo.papermc.io/repository/maven-public/" }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.yml'
    }
}

tasks {
    assemble {
        dependsOn reobfJar
    }
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-folia.jar"

    dependencies {
        exclude(dependency('net.kyori:^(?!adventure-text-feature-pagination).+$'))
    }

    relocate 'net.kyori.adventure.text.feature.pagination', 'me.lucko.spark.lib.adventure.pagination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    project.applyExcludes(delegate)
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
