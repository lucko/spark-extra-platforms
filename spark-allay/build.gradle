plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'org.allaymc.gradle.plugin' version '0.1.2'
}

tasks.withType(JavaCompile).configureEach {
    // override, compile targeting J21
    options.release = 21
}

allay {
    api = "0.17.0"

    plugin {
        it.entrance = "me.lucko.spark.allay.AllaySparkPlugin"
        it.name = "spark"
        it.description = project.pluginDescription
        it.authors = ["Luck", "IWareQ", "daoge_cmd"]
        it.version = project.pluginVersion
        it.website = "https://spark.lucko.me/"
    }
}

dependencies {
    implementation "me.lucko:spark-common:${project.baseVersion}-SNAPSHOT"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.json'
    }
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-allay.jar"

    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.kyori.option', 'me.lucko.spark.lib.adventure.option'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    project.applyExcludes(delegate)
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
